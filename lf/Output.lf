target C;

/**
 * Prints the outputs of the Furuta pendulum simulation
 * into a CSV file.
 * 
 * @author Edward A. Lee
 * @author Alexander Schulz-Rosengarten
 */
reactor Output(filename:string("pendulum-sample.csv")) {
    input control:double;
    input led:bool;
    input speaker:bool;
    input energy:double; 
    
    input theta:double;
    input d_theta:double;
    input phi:double;
    input d_phi:double;

    state file:FILE*({=NULL=}); // The output file
    
    reaction(startup) {=
        self->file = fopen(self->filename, "w");
        if(self->file == NULL) {
            lf_print_error_and_exit("Failed to open file: %s", self->filename);
        } else {
            fprintf(self->file, "Time,PTime,LED,Speaker,Control,Energy,Theta,dTheta,Phi,dPhi\n");
        }
    =}
    
    reaction(control, led, speaker, energy, theta, d_theta, phi, d_phi) {=
        double lt = lf_time_logical_elapsed() / 1.0e9;
        double pt = lf_time_physical_elapsed() / 1.0e9;
        fprintf(self->file, "%f,%f,%d,%d,%.4f,%.8f,%.8f,%.8f,%.8f,%.8f\n",
            lt, pt,
            led->value, speaker->value, control->value, energy->value,
            theta->value, d_theta->value, phi->value, d_phi->value
        );
    =}
    
    reaction(shutdown) {=
        fclose(self->file);
    =}
}