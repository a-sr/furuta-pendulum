#resource "../../impl/behavior.h"
#resource "../../impl/behavior.c"
#resource "../../impl/simulation.h"
#resource "../../impl/simulation.c"
#hostcode-c "#include \"simulation.h\""
#hostcode-c "#include \"behavior.h\""

/**
 * This SCChart provides a simple simulation environment for the
 * Furuta pendulum.
 * 
 * The simulation logic is factored out (impl/simulation.c) and 
 * is based on a Ptolemy II model constructed by Johan Eker and 
 * described in this paper:
 * J. Liu, J. Eker, J. W. Janneck, and E. A. Lee,
 * “Realistic simulations of embedded control systems,”
 * IFAC Proceedings Volumes, vol. 35, no. 1, pp. 391–396, 2002.
 *
 * Original adaption to C and LF by Edward A. Lee, see:
 * https://github.com/lf-lang/examples-lingua-franca/tree/383e816627e2668330df3dfc08e7f4202461f42c/C/src/modal_models/FurutaPendulum
 *
 * It uses a simple forward-Euler simulation.
 * The Ptolemy II model is more accurate because it uses an
 * RK-45 solver.
 * 
 * This model outputs its state every `sample_period`.
 * It updates the state before outputting it using the most 
 * recently received control input, except on the first input, 
 * where it just outputs the initial state.
 * 
 * The `theta` output is the angle of the pendulum, which is 0
 * when the pendulum is pointing straight up, and `d_theta` is 
 * its initial angular velocity. The `phi` output is the angle 
 * of the horizontal arm and `d_phi` is its angular velocity.
 * 
 * @author Alexander Schulz-Rosengarten
 */
scchart PendulumSimulation {
    input float control
    input float sample_period
    input float disturbance
    
    output float theta
    output float d_theta
    output float phi
    output float d_phi
    
    const float initial_theta = -3.14159 // Initial pendulum angle (down)
    
    // External funtion for the simulation
    @Hide extern "simulate" simulate
    
    float pendulum_state[4] = {0,0,0,0} // Internal state of the pendulum.
    float latest_control = 0 // Stores the controller response until next simulation cycle.
    
    // Initialize pendulum state
    entry do pendulum_state[0] = initial_theta
    // Simulate (except in first tick)
    during do pendulum_state[0] += disturbance; // Apply disturbance
              simulate(&pendulum_state, sample_period, latest_control);
              latest_control = control // store control response
    // Produce output
    immediate during 
        do theta = pendulum_state[0]; 
           d_theta = pendulum_state[1];
           phi = pendulum_state[2];
           d_phi = pendulum_state[3]
}

/**
 * Computes the energy based on the given angle using
 * the implementation in "behavior.c".
 * 
 * @author Alexander Schulz-Rosengarten
 */
scchart PendulumEnergy {
    input float theta
    input float d_theta
    
    output float energy

    // External funtions for computation
    @Hide extern "restrict_angle" restrict_angle
    @Hide extern "get_energy" get_energy
        
    immediate during do energy = get_energy(d_theta, restrict_angle(theta))
}
