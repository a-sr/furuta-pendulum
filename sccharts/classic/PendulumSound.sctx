#resource "../../impl/behavior.h"
#resource "../../impl/behavior.c"
#hostcode-c "#include \"behavior.h\""

/**
 * This SCChart produces a square wave sound signal relative to 
 * the angle of the pendulum.
 * The logic for determining the frequency is factored out
 * (see impl/behavior.c).
 * The model uses only classical SCCharts elements and the msec
 * input to track time.
 * 
 * @author Alexander Schulz-Rosengarten
 */
scchart PendulumSound {
  input float angle
  input bool msec
  
  output bool sound = true

  // Conversion factor for frequency to msec
  const int SEC_TO_MSEC = 1000
  // External funtion for the sound behavior
  @Hide extern "sound_frequency" sound_frequency
  
  int msecs = 0 // Number of msecs passed
  float duration = 0 // Duration of a cycle
  
  // Update cycle duration based on angle
  immediate during do duration = ((1 / sound_frequency(angle)) / 2) * SEC_TO_MSEC
  
  region {
        initial state Alternate {
            // Cound msecs
            during if msec do msecs++
        } if msecs >= duration // If reached duration...
            do sound = !sound; msecs = 0 // toggle and reset
            go to Alternate
  }
}
